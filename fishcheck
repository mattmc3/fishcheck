#!/usr/bin/env fish

function _fishcheck__version
    echo "fishcheck - Fish script analysis tool"
    echo "version: 0.0.1-alpha"
    echo "license: MIT"
    echo "website: https://github.com/brinysea/fishcheck"
end

function _fishcheck__help
    echo "Usage: fishcheck [OPTIONS...] FILES..."
    echo "  -h, --help                     Show this usage summary and exit"
    echo "  -v, --version                  Print version information"
    echo "  -s, --severity=SEVERITY        Minimum severity for checks (default: error)"
    echo "                                 Severities: error, warning, info, style"
end

function _fishcheck_draw_line_pointer
    if test (count $argv) -ne 2 || test $argv[1] -le 0 2>/dev/null || test $argv[2] -ne $argv[2] 2>/dev/null
        return 1
    end
    echo -n (string pad -c ' ' -w $argv[1] '^')
    if test $argv[2] -gt 1
        echo -n (string pad -c '~' -w (math $argv[2] - 1) '^')
    end
    echo
end

function _echo_section
    echo (set_color --bold)$argv[1]": "(set_color normal)$argv[2..]
end

function _fishcheck_report
    set -l optspec f/file= c/check= m/message= n/lineno= l/line= fix= \
        S/start= L/length= did-you-mean=

    argparse $optspec -- $argv

    set -l checkpat '^FC([EWIS])\d{4}'
    if not string match -rq -- $checkpat $_flag_check
        echo "fishcheck: Report expecting valid check ID with '--check' flag" >&2 && return 1
    end
    set -l check_sevcode (string match -rg -- $checkpat $_flag_check)
    switch $check_sevcode
        case E
            set -f severity error
            set -f check_color (set_color red)
        case W
            set -f severity warning
            set -f check_color (set_color yellow)
        case I
            set -f severity info
            set -f check_color (set_color green)
        case S
            set -f severity style
            set -f check_color (set_color blue)
    end

    set -l check_desc (functions -Dv $_flag_check)[5]

    # check:
    echo "$check_color$_flag_check ($severity): $check_desc"(set_color normal)
    _echo_section docs https://github.com/mattmc3/fishcheck/wiki/$_flag_check
    # file:
    if test -n "$_flag_lineno"
        _echo_section file "$_flag_file (line $_flag_lineno)"
    else
        _echo_section file $_flag_file
    end
    # message:
    if set -q _flag_message
        _echo_section message $_flag_message
    end
    if test -n "$_flag_line"
        echo $_flag_line
    end
    if test -n "$_flag_start"
        _fishcheck_draw_line_pointer $_flag_start $_flag_length
    end
    if test -n "$_flag_did_you_mean"
        _echo_section "Did you mean?" (set_color cyan)$_flag_did_you_mean(set_color normal)
    end
    if test -n "$_flag_fix"
        _echo_section fix (set_color cyan)$_flag_fix(set_color normal)
    end
    echo
end

function fishcheck
    set -l fishcheck_version 0.0.1
    set -l optspec h/help v/version s/severity=
    argparse --name fishcheck $optspec -- $argv
    or return 1

    if set -q _flag_version
        _fishcheck__version && return
    else if set -q _flag_help
        _fishcheck__help && return
    else if test (count $argv) -eq 0
        echo "fishcheck: No files specified. 'fishcheck -h' for help." >&2 && return 1
    end

    set -l checkfuncs
    for checkfile in (path resolve (status dirname))/checks/*.fish
        source $checkfile
        set -l checkfunc (path change-extension '' (path basename $checkfile))
        test $checkfunc = FCE1000 && continue
        set --append checkfuncs $checkfunc
    end

    # We always run FCE1000, but skip all other checks on broken Fish files
    set --local parsable_fishfile
    for fishfile in $argv
        FCE1000 $fishfile
        test $status -eq 0 && set --append parsable_fishfile $fishfile
    end

    for fishfile in $parsable_fishfile
        for checkfunc in $checkfuncs
            $checkfunc $fishfile
        end
    end
end

fishcheck $argv
